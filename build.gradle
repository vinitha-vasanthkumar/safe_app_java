// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.github.kt3k.coveralls' version '2.8.2'
}
allprojects {
    repositories {
        google()
        jcenter()
    }
}
subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    checkstyle {
        toolVersion "7.6.1"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    pmd {
        ruleSets = ["$rootDir/config/pmd/pmd-ruleset.xml"]
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    tasks.withType(Checkstyle) {
        exclude "**/net/maidsafe/safe_app/*"
        exclude "**/net/maidsafe/safe_authenticator/*"
        reports {
            html.enabled = true
            xml.enabled = false
        }
    }
}
task delete(type: Delete) {
    delete rootProject.buildDir
}

task Download(dependsOn: ['safe-app:download-nativelibs', 'safe-app-android:download-nativelibs']) {
    // configuration
}

coveralls {
    jacocoReportPath 'safe-app/build/reports/jacoco/test/jacocoTestReport.xml'
    sourceDirs = ["safe-app/src/main/java", "api/src/main/java"]
}

tasks.coveralls {
    dependsOn ':safe-app:jacocoTestReport'
}
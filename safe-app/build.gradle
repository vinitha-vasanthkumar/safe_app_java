import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import de.undercouch.gradle.tasks.download.Download
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: "com.github.johnrengelman.shadow"

group 'net.maidsafe'
version '0.1.0'

dependencies {
    implementation project(':lib')
    implementation project(':api')
    implementation project(':mock')
    testImplementation 'junit:junit:4.12'
}
repositories {
    mavenCentral()
}
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath 'de.undercouch:gradle-download-task:3.2.0'
    }
}

task copyTestResources(type: Copy) {
    def platform = "linux"
    def x64Versions = ["x86_64", "amd64", "x64", "x86-64","ia-64", "ia64", "ppc64", "sparc64", "ultrasparc", "sparc-v9", "arm64", "arm-v8"]
    def arch = x64Versions.find { it == "x64" } == null ? "x86" : "x64"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        platform = "win"
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        platform = "osx"
    }
    from "${projectDir}/libs/${platform}/mock/${arch}"
    into "${buildDir}/classes/java/test/native"
}
processTestResources.dependsOn copyTestResources

def nativeLibsVersion = "nikita-d67a875"
def tempDownloadDir =  "${projectDir}/.tempDownloads"

static def getCombinations() {
    def platforms = ['osx', 'linux', 'win']
    def variants = ['mock', 'non-mock']
    def architectures = ['x64'] // ['x86', 'x64']
    [platforms, architectures, variants].combinations().findAll { !(it[0] == 'osx' && it[1] == 'x86') }
}
tasks.addRule('Pattern: NativeLibs-<ID>') { String taskName ->
    if (taskName.startsWith('NativeLibs-')) {
        task(taskName, type: Copy) {
            dependsOn << [ name.replace('NativeLibs', 'delete') ]
            def args = name.split('-')
            String platform = args[1]
            String arch = args[2]
            from "${projectDir}/libs/${platform}/${args[3] == 'mock'? 'mock' : 'non-mock'}/${arch}/"
            into "${projectDir}/src/main/resources/native"
        }
    }
}
tasks.addRule('Pattern: delete-<ID>') { String taskName ->
    if (taskName.startsWith('delete-')) {
        task(taskName, dependsOn: build, type: Delete) {
            delete "${projectDir}/src/main/resources/native/"
            delete "${projectDir}/build/resources/main/native/"
            delete "${projectDir}/build/classes/java/test/native"
        }
    }
}
tasks.addRule('Pattern: pack-<ID>') { String taskName ->
    if (taskName.startsWith('pack')) {
        task(taskName, type: ShadowJar) {
            dependsOn << [ name.replace('pack', 'NativeLibs') ]
            def args = name.split('-')
            String platform = args[1]
            String arch = args[2]
            boolean isMock = args[3] == "mock"
            from sourceSets.main.output
            from sourceSets.main.resources
            from project(':api').sourceSets.main.output
            from project(':api').sourceSets.main.resources
            from project(':lib').sourceSets.main.output
            from project(':lib').sourceSets.main.resources
            if (isMock) {
                from project(':mock').sourceSets.main.output
                from project(':mock').sourceSets.main.resources
            }
            baseName = "${project.name}${isMock ? '-mock' : ''}"
            classifier = "${platform}-${arch}"
            version = project.version
            doLast {
                println "Completed packaging ${name}"
            }
        }
    }
}
task ('pack') {
    def tasks = getCombinations().collect {"pack-${it[0]}-${it[1]}-${it[2]}" }
    dependsOn << tasks
    doLast {
        println "Deleting default Jar..."
        File defaultJar = new File("${project.buildDir}/libs/safe-app-${project.version}.jar")
        if (defaultJar.exists()) {
            defaultJar.delete()
        }
    }
}
task ("downloadNativeLibs", type: Download) {
    File dir = new File(tempDownloadDir)
    if (dir.exists()) {
        assert dir.deleteDir()
    }
    def endPoints = getCombinations().collect {
//        "https://s3.eu-west-2.amazonaws.com/safe-client-libs/safe_app-${it[2] == 'mock' ? 'mock-' : ''}${nativeLibsVersion}-${it[0]}-${it[1]}.zip"
        "https://github.com/krishnaIndia/safe_core/releases/download/${nativeLibsVersion}/safe_app-${it[2] == 'mock' ? 'mock-' : ''}${nativeLibsVersion}-${it[0]}-${it[1]}.zip"
    }
    src(endPoints)
    dest tempDownloadDir
}
tasks.addRule('Pattern: unzip-<ID>') { String taskName ->
    if (taskName.startsWith('unzip')) {
        task(taskName, type: Copy) {
            def args = name.split('-')
            String platform = args[1]
            String arch = args[2]
            boolean isMock = args[3] == "mock"
            from zipTree("${tempDownloadDir}/safe_app-${isMock ? 'mock-' : ''}${nativeLibsVersion}-${platform}-${arch}.zip")
            into "${projectDir}/libs/${platform}/${isMock ? 'mock' : 'non-mock'}/${arch}"
        }
    }
}
task ("download-nativelibs") {
    dependsOn << ['downloadNativeLibs', getCombinations().collect { "unzip-${it[0]}-${it[1]}-${it[2]}" }]
}
